apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply from: "$project.rootDir/secretconfig.gradle"

android {
    compileSdkVersion build_versions.target_sdk
    defaultConfig {
        applicationId "com.lounah.usefulauthwithweather"
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode 1
        versionName "0.1.1 от " + getDate()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField("String", "WEATHER_API_ENDPOINT", "\"https://api.openweathermap.org/\"")
    }

    lintOptions {
        checkReleaseBuilds false
    }

    signingConfigs {
        config {
            keyAlias signConfigs.keyAlias
            keyPassword signConfigs.keyPassword
            storeFile file(signConfigs.storeFile)
            storePassword signConfigs.storePassword
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            multiDexEnabled true
            minifyEnabled false
            testCoverageEnabled true
        }
        applicationVariants.all { variant ->
            variant.buildConfigField "String", "WEATHER_API_KEY", "\""+apiKeys.weather+"\""
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }
    androidExtensions {
        experimental = true
    }
}

dependencies {
    // Base
    implementation deps.kotlin.stdlib
    implementation deps.support.app_compat
    implementation deps.support.support
    implementation deps.support.design
    implementation deps.support.constraint

    // VM
    implementation deps.arch.view_model
    implementation deps.arch.lifecycle_extension

    // Retrofit
    implementation deps.retrofit.runtime
    implementation deps.retrofit.rxadapter
    implementation deps.retrofit.factory

    // RxJava
    implementation deps.rx.rxjava
    implementation deps.rx.rxandroid

    // Dagger
    implementation deps.dagger.runtime
    implementation deps.dagger.support
    kapt deps.dagger.processor
    kapt deps.dagger.compiler

    // Testing
    testImplementation deps.tests.junit
    testImplementation deps.tests.hamcrest
    testImplementation deps.tests.mockito
    testImplementation deps.tests.robolectric
    testImplementation deps.arch_core.testing

    androidTestImplementation deps.tests.espressoContrib
    androidTestImplementation deps.tests.androidTestRules
    androidTestImplementation deps.tests.espressoCore
    androidTestImplementation deps.atsl.runner
}

static def getDate() {
    return new Date().format('yyyy/MM/dd')
}